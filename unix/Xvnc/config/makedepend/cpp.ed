#
# $Xorg: cpp.ed,v 1.3 2000/08/17 19:41:50 cpqbld Exp $
#
# $Locker $
#
/struct symtab stab/d
/struct symtab \*defloc;/d
/struct symtab \*udfloc;/d
/struct symtab \*incloc;/d
/struct symtab \*ifloc;/d
/struct symtab \*elsloc;/d
/struct symtab \*eifloc;/d
/struct symtab \*ifdloc;/d
/struct symtab \*ifnloc;/d
/struct symtab \*ysysloc;/d
/struct symtab \*varloc;/d
/struct symtab \*lneloc;/d
/struct symtab \*ulnloc;/d
/struct symtab \*uflloc;/d
/^sayline(/s/$/	DELETED/p
.,/^}/d
/^unfill(/s/$/	DELETED/p
.-1,/^}/d
/^doincl(/s/$/	DELETED/p
.-1,/^}/d
/^equfrm(/s/$/	DELETED/p
.,/^}/d
/^dodef(/s/$/	DELETED/p
.-1,/^}/d
/^control(/s/$/	DELETED/p
.-1,/^}/d
/^savestring(/s/$/	DELETED/p
.-1,/^}/d
/^stsym(/s/$/	DELETED/p
.-1,/^}/d
/^ppsym(/s/$/	DELETED/p
.-1,/^}/d
/^yyerror(/s/$/	DELETED/p
.,/^}/d
/^ppwarn(/s/$/	DELETED/p
.,/^}/d
/^lookup(/s/$/	DELETED/p
.-1,/^}/d
/^subst(/s/$/	DELETED/p
.-1,/^}/d
/^trmdir(/s/$/	DELETED/p
.-1,/^}/d
/^copy(/s/$/	DELETED/p
.-1,/^}/d
/^pperror(/s/$/	DELETED/p
.,/^}/d
/^main(/s/$/	CHANGED to cpp_varsetup(argc,argv)/p
.c
cpp_varsetup(argc,argv)
.
/^strdex(/s/$/	DELETED/p
.-1,/^}/d
/^ for(i=1; i<argc; i++)/s/$/	To the end DELETED/p
.,$c
}
.
/^dump(/s/$/	DELETED/p
.,/^}/c
dump() { fatal("dump\n"); }
.
/^refill(/s/$/	DELETED/p
.,/^}/c
refill() { fatal("refill\n"); }
.
/^slookup(/s/$/	DELETED/p
.-1,/^}/c
static struct symtab *slookup() { fatal("slookup\n"); }
.
w
q
